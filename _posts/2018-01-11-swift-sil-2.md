---
layout: post
title:   Swiftの中間言語SILを読む その2 - box-to-stack optimization
---

どういう進め方をしようか少し迷ったけれど、SILのSyntaxなどはすっ飛ばしてまず最適化を1つ読んでみることにした。Syntaxはわからなければそのたびにドキュメント見ればだいたいわかりそうなのと記事にしたとしてもドキュメントの焼き直しくらいにしかならなさそうなので、進めながらわからなかったところ・知らなかったところをメモする程度にして、このシリーズでは主に最適化を順番に読んでいくことにした。

まずは練習がてら、前回紹介した [この動画](https://www.youtube.com/watch?v=Ntj8ab-5cvE)でも少し触れられているbox-to-stack optimizationを読んでみる。

## box-to-stack optimization 概要

ファイルとしては`lib/SILOptimizer/AllocBoxToStack.cpp`。
**box-to-stack optimization** とか **alloc_box Promotion** とかいう名前で呼ばれている通り、SILの`alloc_box`命令を`alloc_stack`命令に変える(つまりアロケート先をヒープからスタックに**Promote**する)最適化のこと。

box-to-stack optimizationは**Guaranteed Optimization**である。

## なぜ必要なのか？

Swiftでは関数内で宣言した変数が必ずしもスタックに載せることができて、関数のスコープを抜けたタイミングで破棄できる**とは限らない。**



なぜかというとSwiftはClosureをサポートしており、変数がClosureによってキャプチャされ、変数のライフタイムが関数のスコープより長くなる(**escape**する)可能性があるからである。

```swift
func myFunction() -> Int {
   var x = 1
   runClosure { x = 2 }
   return x
}
```

(ちなみにスタックに載せられる・載せられないを**stack discipline**に従うとかいうみたいなのだが、いまだになんて訳すべきなのかはわからない)

このためSwiftとしてはSILGenの時点(つまりraw SIL)では全ての変数は一旦`alloc_box`命令によって**ヒープにアロケートする**ことにして、使われ方を確認したのち本当にローカルな変数なのであれば**最適化によってスタックへのアロケートに変更する**という動きをする。

## 最適化の動きをみてみる

```swift
func myFunction() -> Int {
  var a = 1
  return a
}
```

このシンプルなコードを例に `-sil-print-all`で最適化の過程をのぞいてみる。

```
$ swiftc -Onone -Xllvm -sil-print-all -Xllvm -sil-print-only-functions=myFunctionSiy bts.swift &> bts.sil
```

すると`Stack Promotion of Box Objects` という名前のPassの前後で`alloc_box` から `alloc_stack`に変わっていることが確認できる。

```swift
// myFunction()
sil hidden @_T03bts10myFunctionSiyF : $@convention(thin) () -> Int {
bb0:
  %0 = alloc_box ${ var Int }, var, name "a"      // users: %10, %1
  %1 = project_box %0 : ${ var Int }, 0           // users: %7, %6
  // function_ref Int.init(_builtinIntegerLiteral:)
  %2 = function_ref @_T0S2iBi2048_22_builtinIntegerLiteral_tcfC : $@convention(method) (Builtin.Int2048, @thin Int.Type) -> Int // user: %5
  %3 = metatype $@thin Int.Type                   // user: %5
  %4 = integer_literal $Builtin.Int2048, 1        // user: %5
  %5 = apply %2(%4, %3) : $@convention(method) (Builtin.Int2048, @thin Int.Type) -> Int // user: %6
  store %5 to [trivial] %1 : $*Int                // id: %6
  %7 = begin_access [read] [static] %1 : $*Int    // users: %9, %8
  %8 = load [trivial] %7 : $*Int                  // user: %11
  end_access %7 : $*Int                           // id: %9
  destroy_value %0 : ${ var Int }                 // id: %10
  return %8 : $Int                                // id: %11
} // end sil function '_T03bts10myFunctionSiyF'

*** SIL function after Guaranteed Passes "Stack Promotion of Box Objects" (1) ***
// myFunction()
sil hidden @_T03bts10myFunctionSiyF : $@convention(thin) () -> Int {
bb0:
  %0 = alloc_stack $Int, var, name "a"            // users: %9, %5, %6
  // function_ref Int.init(_builtinIntegerLiteral:)
  %1 = function_ref @_T0S2iBi2048_22_builtinIntegerLiteral_tcfC : $@convention(method) (Builtin.Int2048, @thin Int.Type) -> Int // user: %4
  %2 = metatype $@thin Int.Type                   // user: %4
  %3 = integer_literal $Builtin.Int2048, 1        // user: %4
  %4 = apply %1(%3, %2) : $@convention(method) (Builtin.Int2048, @thin Int.Type) -> Int // user: %5
  store %4 to [trivial] %0 : $*Int                // id: %5
  %6 = begin_access [read] [static] %0 : $*Int    // users: %8, %7
  %7 = load [trivial] %6 : $*Int                  // user: %10
  end_access %6 : $*Int                           // id: %8
  dealloc_stack %0 : $*Int                        // id: %9
  return %7 : $Int                                // id: %10
} // end sil function '_T03bts10myFunctionSiyF'
```


クロージャによってキャプチャされる場合は必ずpromoteされるかと言われるとそんなことはなくて、`@escaping`の有無によってpromoteされるかされないか決まる。

`@escaping`がない場合は最適化される。

```swift
func runClosure(_ f: () -> ()) {
  f()
}

func myFunction() -> Int {
  var x = 1
  runClosure { x = 2 }
  return x
}
```

```swift
%0 = alloc_stack $Int, var, name "x"
```


`@escaping`をつけるとこの最適化はされない。
(ちなみにどの`alloc_box`も最適化ができない場合、`-sil-print-all`での出力はスキップされる。)

```swift
func runClosure(_ f: @escaping () -> ()) {
  f()
}
```

```swift
%0 = alloc_box ${ var Int }, var, name "x"
```

動きはわかったので、次は実装である`AllocBoxToStack.cpp`を読んでみる。

## メインのロジックをみてみる

最適化は`run`という関数を持った`SILFunctionTransform`を継承してつくられ、`run`内で`SIL`を書き換えていくことで行われていく。このクラスがおそらく`PassManager.cpp`によって管理されている？

```cpp
class AllocBoxToStack : public SILFunctionTransform {
  /// The entry point to the transformation.
  void run() override { ... }
}
```

メインの実装はシンプルで、関数内の`AllocBoxInst`命令を探して、`canPromoteAllocBox` でチェックして、promoteできそうなら`Promotable`に記録する。

```cpp
for (auto &BB : *getFunction()) {
  for (auto &I : BB)
    if (auto *ABI = dyn_cast<AllocBoxInst>(&I))
      if (canPromoteAllocBox(ABI, PromotedOperands))
        Promotable.push_back(ABI);
}
```

そして`rewritePromotedBoxes`で書き換える。

```cpp
if (!Promotable.empty()) {
  bool CFGChanged = false;
  auto Count = rewritePromotedBoxes(Promotable, PromotedOperands,
                                    CFGChanged);
```

メインの流れはこんな感じなので、あとはそれぞれ

+ promoteできるかどうかの判断のロジック
+ 書き換えのロジック

を追ってみる。


## Promoteできるかどうかの判断のロジック

`canPromoteAllocBox` / `findUnexpectedBoxUse` 辺りの関数によって行われている。

まず、SILモジュールの機能として、SILValue(`%1` のようなやつ)が何かの命令(`SILInstruction`, `alloc_box`など) のオペランド(`Operand`)になっているかを取れ、どこで使われているかを確認できる。
ちなみにSIL上にも`users:`で表示されている。

```swift
%0 = alloc_box ${ var Int }, var, name "x"      // users: %16, %9, %1
```

まず`Box`であることに起因する命令、例えば参照カウントを増やす・減らすなどの命令は無視できる。

```cpp
if (isa<StrongRetainInst>(User) || isa<StrongReleaseInst>(User) ||
    isa<ProjectBoxInst>(User) || isa<DestroyValueInst>(User) ||
    (!inAppliedFunction && isa<DeallocBoxInst>(User)))
  continue;
```

変数がコピーされている場合はその先も追っていく必要があるので、チェック用のリストに追加して再帰的にチェックする。

```cpp
if (isa<MarkUninitializedInst>(User) || isa<CopyValueInst>(User)) {
  copy(cast<SingleValueInstruction>(User)->getUses(),
       std::back_inserter(Worklist));
  continue;
}
```

`@escaping`によってエスケープする場合はまずココに来る。クロージャを作る前に`copy_value`によって参照がコピーされ、`partial_apply`でクロージャを作る際のコンテキストとして渡される。

```swift
%9 = copy_value %0 : ${ var Int }               // user: %11
mark_function_escape %1 : $*Int                 // id: %10
%11 = partial_apply %8(%9) : $@convention(thin) (Bool, @owned { var Int }) -> () // user: %12
```

そこからは作ったクロージャ自体やその使われ方をチェックしていく。

`checkPartialApplyBody` はクロージャのBodyの中での変数の使われ方を見る。クロージャにはコンテキストとして変数が渡されることになるが、その変数について再度`findUnexpectedBoxUse`を呼び出して使われかたを確認している。

`partialApplyEscapes`は`partial_apply`命令で作ったクロージャの使われ方を見る。そのクロージャが他の関数に渡されている場合はいくつかの例外を除きescapeされたとされる。

## 書き換えのロジックをみてみる

`rewriteAllocBoxAsAllocStack`が書き換え用の関数である。やっていることはシンプルでスタックへ書き換えて、要らないものを消す。

具体的には、

+ `alloc_box`を`alloc_stack`に変換する
+ `project_box`命令を消して`alloc_stack`でアロケートしたものを直接見るように書き換える。
+ 最後で使われたところに`dealloc_stack`命令を挿入する
+ 参照カウントの増減などの命令はそのまま消す


## まとめ

まずは基本的な最適化をみてみた。まだSILモジュールでわからないところがちょいちょいあるので読むのにそこそこ時間がかかってしまったが、ある程度は読めたと思う。。

次はもう一つぐらい最適化を読んでみたい。


